<!DOCTYPE HTML>
<html lang="en">
<head>
<meta charset="utf-8">
<meta name="viewport" content="width=device-width, initial-scale=1.0">
<link rel="stylesheet" href="style.css">
	
<title>Tutorial on SageMath</title>

<script src="https://sagecell.sagemath.org/static/embedded_sagecell.js"></script>
<script>
    sagecell.makeSagecell({inputLocation:  '#Activate',
          template: sagecell.templates.minimal,
          linked: true,
          linkKey:"1",
          autoeval:true,
          hide:["fullScreen"],
          evalButtonText: 'Activate'});
    sagecell.makeSagecell({inputLocation: 'div.compute',
          linked: true,
          linkKey:"1",
          hide:["permalinkpermalink","done","sessionFiles"], 
          evalButtonText: 'Evaluate'});
</script>
</head>
<body>

<h2>Tutorial on SageMath</h2>
	
<h3>Introduction</h3>

<p>
  I will give an introduction of <a href="https://www.sagemath.org/index.html">SageMath</a> here.
  The main features of SageMath:
  <ol>
    <li>SageMath is a <a href="https://en.wikipedia.org/wiki/Computer_algebra_system">computer algebra system</a>;</li>
    <li>SageMath uses a syntax resembling <a href="https://www.python.org/">Python</a>'s;</li>
    <li>SageMath covers many aspects of discrete mathematics.</li>
  </ol>
  The main reference is its <a href="https://doc.sagemath.org/html/en/index.html">official documentation</a>.
</p>

<p>
After installing SageMath, you will have a shortcut to Jupyter (SageMath Notebook). We usually write SageMath code there. 
You can also try SageMath <a href="https://sagecell.sagemath.org/">online</a>. 
</p><p>
In general, most of the commands/methods you need can be found by searching on the internet (very similar to the situation of LaTeX). 
Well, sometimes you can understand the full story only after hundreds of guessing, testing, and comparing so many documents, which is very similar to math research,
	see this <a href="https://www.reddit.com/r/ProgrammerHumor/comments/7r3hvl/if_the_authors_of_programming_textbooks_wrote/" title = "If the authors of computer programming books wrote arithmetic text books">meme</a>.
I think most of readers already get used to this feeling for many years, so let me share with you something different. 
I will focus more on "programming", say how to design your algorithm.
I hope you will feel SageMath is not only an advanced calculator. 


</p>

<!--<div id="Activate" visibility: hidden>
  <script type="text/x-sage">
def my_fun(x): 
	print(x)
print("done")
# put your code
  </script>
</div>-->

<h3>Python syntax</h3>

I highly recommend readers to take a close look at the syntax of Python first. 
This information is readily available in documentation online.
Here are two examples. 

<h4>Collatz conjecture</h4>
<p>Consider the following operation on an arbitrary positive integer:
<a>\[n\longmapsto \begin{cases}
	n/2, & \text{if $n$ is even}, \\
	n+3, & \text{if $n$ is odd}. 
	\end{cases}\]</a>
The Collatz conjecture is: This process will eventually reach the cycle $1\mapsto 4\mapsto 2 \mapsto \cdots$, regardless of which positive integer is chosen initially.
	</p>
<div class="compute">
  <script type="text/x-sage">
num = 100
num_step = 0 # to record the number of steps
while num not in [1,2,4]: 
	num_step += 1 # equivalently, num_step = num_step + 1
	if num/2 in ZZ: # in Sage, ZZ = the ring of integer numbers
		num = num/2
	else:
		num = num + 3
	print("the num is "+str(num))
print("It takes "+str(num_step)+" steps")
</script>
</div>

<h4>Fibonacci sequence</h4>
<p>Fibonacci sequence is the sequence defined by 
\[F_0=0,F_1=1, \text{ and } F_n=F_{n-1}+F_{n-2} \text{ when $n\geq 2$}.\]
We can compute it using the following code. </p>

<div class="compute">
  <script type="text/x-sage">
record={0:0,1:1} # F_0=1, and F_1=1
def Feb(n): # define a function
	if n not in record.keys():
		record[n] = record[n-1] + record[n-2]
	return record[n] 
for n in range(10): 
	print("F_"+str(n)+" is "+str(Feb(n)))
print(record)
</script>
</div>

<!--Note that the difference of list and tuple (a feature of Python). 
<p><div class="compute">
<script type="text/x-sage">
x = [0,1]; y = x; y[0] = 2; 
# Say, what is recorded in a list, is not a value, it is the address of the value
print(x,y) 
x = (0,1); y = x; y[0] = 2 
# one cannot change a value in a tuple directly.
print(x,y) 
</script>
</div>
</p>-->

<h3>Selected Structures in Mathematics
</h3>
Here I give some typical examples of mathematical structures. We refer to the <a href="https://doc.sagemath.org/html/en/index.html">documents</a>, 
  especially <a href="https://doc.sagemath.org/html/en/constructions/index.html">construction</a> for more structures. 
  
<h4>Matrices</h4>

<div class="compute">
  <script type="text/x-sage">
M = Matrix([[binomial(i,j) for j in [0..4]] for i in [0..4]])
N = Matrix([[(-1)^(i+j)*binomial(i,j) for j in [0..4]] for i in [0..4]])
# Note that in Python, everything starts from 0, including matrices
print(det(M*N))
print(M[2][1],N[4,2])
</script>
</div>

<h4>Permutations</h4>

<div class="compute">
  <script type="text/x-sage">
G=Permutations(3) 
x = G([1,3,2]); y = G([2,1,3])
print(x*y,(x*y).length()) 
print(list([x(y(i)) for i in [1..3]]))
# In SageMath, permutations are reversed. 
</script>
</div>



<h4>Polynomials</h4>

<div class="compute">
  <script type="text/x-sage">
R = PolynomialRing(QQ,["x"+str(i) for i in [0..3]])
x = list(R.gens())
f = 2*x[0]^2*x[2] + R.monomial(*[4-i for i in [0..3]])
print(dict(f))
# However, the keys of f.dict() is an ETtuple, and SageMath forgets to include a constructor and overload operators with the usual tuples. 
My_dict = f.dict()
My_dict = {tuple(x): My_dict[x] for x in My_dict}
print((2,0,1,0) in f.dict().keys())
print((2,0,1,0) in My_dict.keys())
</script>
</div>

	<br>

<div class="compute">
  <script type="text/x-sage">
R = PolynomialRing(QQ,["x"+str(i) for i in [0..3]])
x = list(R.gens())
def divided_difference(a,b=None):
	if b==None: b=a+1
	def my_fun(f):
		f=R(f) # make it work even when f is a constant...
		newx = x+[] # this makes a copy of x, this is necessary! 
		(newx[a],newx[b])=(x[b],x[a])
		res = (f - f(newx))/(x[a]-x[b])
		return R(res) 
		# still, this makes Sage believe res is in R, instead of Frac(R)...
	return my_fun 
print(divided_difference(1)(x[1]^3))
print(divided_difference(1,3)(x[1]^3))
print(divided_difference(0,3)(1))
</script>
</div>

<h4>Free modules</h4>

<div class="compute">
  <script type="text/x-sage">
G = Permutations()
X = CombinatorialFreeModule(QQ,G,prefix="e")
e = X.basis()
f = e[G([1,3,2])]+2*e[Permutation([(1,2,5),(3,4)])]
print(dict(f))
# You have to use hashable classes
</script>
</div>

<h3>Operators</h3>

There are many operators in combinatorics. Here are two examples. 

<h4>Schur operators</h4>
<p>For a partition \(\lambda\), we define \(u_i\) the operator 
\[u_i\lambda = \begin{cases}
\lambda\cup\text{a box in the $i$-th row},& \text{if it is a partition},\\
0, & \text{otherwise}. 
\end{cases}\]
This operator extends to the free vector space of partitions linearly. 
The Schur operators satisfy the relation of <a href="https://en.wikipedia.org/wiki/Plactic_monoid">Plactic monoid</a>.
	</p>

<div class="compute">
  <script type="text/x-sage">
X = CombinatorialFreeModule(QQ,Partitions(),prefix="")
def B(*my_list): 
    # with *, we use B(2,1), without *, we use B([2,1])
    return X.monomial(Partition(my_list)) 
	# Note that Partition != Partitions.  
def single_Schur_op(i): # adding a box in the i-th row (i = 0, 1, ...);
    def my_fun(x):
        res = X(0)
        my_dict = dict(x)
        for partition in my_dict: 
            new_partition = list(partition)
            if len(new_partition)>i and (i==0 or new_partition[i-1]!=new_partition[i]):
                # if we can add a box in the i-th row
                new_partition[i] +=1
                res += my_dict[partition]*B(*new_partition)
        return res
    return my_fun
def Schur_op(*i_list): 
    if len(i_list)==0: 
        return lambda x:x # lambda x:x is the function sending x to x, i.e. identity function
    return lambda x: single_Schur_op(i_list[0])(Schur_op(*i_list[1:])(x))
print(Schur_op(1,2,1,0)(B(2,2,1)+B(4,3,3)))
</script>
</div>

<h4>Dunkl elements</h4>
<p>For fixed \(n\), the Chevalley formula gives 
\[x_k\mathfrak{S}_w = 
	-\sum_{\begin{subarray}{c}i&lt;k\\\ell(wt_{ik})=\ell(w)+1\end{subarray}}\mathfrak{S}_{wt_{ik}}
	+\sum_{\begin{subarray}{c}i&gt;k\\\ell(wt_{ik})=\ell(w)+1\end{subarray}}\mathfrak{S}_{wt_{ik}}.									 \]
where \(\mathfrak{S}_w\) is the Schubert polynomial, and \(t_{ab}\) is the transposition of $a,b$.
Dunkl element is the operator formally defined as above. 
</p>
	
<div class="compute">
  <script type="text/x-sage">
n=4
G = Permutations(n)
X = CombinatorialFreeModule(QQ,G,prefix="S")
def B(*my_list):
    return X.monomial(Permutation(list(my_list)))
def tran(i,j): 
    t_ij = [i for i in [1..n]]
    (t_ij[i-1],t_ij[j-1]) = (j,i)
    t_ij = Permutation(t_ij)
    return t_ij
def Dunkl(k): 
    def my_func(x=X.monomial(G.one())): 
        res = X(0)
        my_dict = dict(x)
        for w in my_dict: 
            for i in [1..k-1]:
                new_w = tran(i,k)*w # compute wt_ik (SageMath use reversed order)
                if new_w.length() == w.length() + 1:
                        res -= my_dict[w] * X.monomial(new_w)
            for i in [k+1..n]:
                new_w = tran(i,k)*w # compute wt_ik (SageMath use reversed order)
                if new_w.length() == w.length() + 1:
                        res += my_dict[w] * X.monomial(new_w)
        return res
    return my_func    
print(Dunkl(1)(Dunkl(3)()))
print(Dunkl(3)(Dunkl(1)()))
</script>
</div>

	<br>
	
<div class="compute">
  <script type="text/x-sage">
# Please run the previous cell
R = PolynomialRing(QQ,["x"+str(i) for i in [1..n]])
x = list(R.gens()) # note that x[i-1] = xi
def Horner(f): # return (i,f1,f2) such that f(x[0],...)=x[i]*f1(x[i],...)+f2(x[i+1],...)
    x = list(f.parent().gens())
    i = 0; f1 = 0; f2 = f
    while f == f2 and i <= len(x): 
        i += 1
        sub = [x[j] if j>i else 0 for j in range(len(x))]
        f2 = f(sub)
    if i >= len(x): # f(x[0],...) == f(0), so f is a contant
        return (None,0,f.parent().base_ring()(f2))
    else: # otherwise, return (i,f1,f2)
        return (i,R((f-f2)/x[i]),f2)
# f = x[1]*x[2]
# (i,f1,f2)=Horner(f)
# f == x[i]*f1+f2
def Mult(f): 
    f=R(f)
    (i,f1,f2) = Horner(f)
    if i==None: 
        return lambda x = X.monomial(G.one()) : f2*x
    else:
        return lambda x = X.monomial(G.one()): Mult(f1)(Dunkl(i)(x))+Mult(f2)(x)
Mult(x[1]*x[2])()
</script>
</div>


<h3>Generating combinatorial objects
</h3>
One advantage of Python is the generator, which provides a powerful tool to generate objects. Here we give two examples in combinatorics. 

<h4>Multi-sets</h4>
<p>A multi-set with alphabet \(L\) of cardinality \(k\) is an \(S_k\)-orbit of \(L^k\).
When \(L\) is ordered, we can uniquely write such multi-set by \(\{\!\{i_1\leq \cdots\leq i_k\}\!\}\) with all \(i_j\in L\).
</p>
<div class="compute">
  <script type="text/x-sage">
def Msets(L,k):
    # boundary cases:
    # when k == 0, only empty multi-set
    # when L is empty and k!=0, no multi-set
    if k == 0: 
        yield []
        return
    if len(L) == 0:
        return 
    # if the multi-set contains L[0]
    for A in Msets(L,k-1): 
        yield [L[0]]+A
    # if the multi-set does not contain L[0]
    for A in Msets(L[1:],k):
        yield A

for A in Msets([1,2,3],4):
    print(A)
</script>
</div>



<h4>Standard Young Tableaux</h4>

<p>Let \(\lambda\) be a partition. A standard Young tableau (SYT) of \(\lambda\) is a filling of its Young diagram with \(\{1,\cdots,n\}\) for \(n\) the number of boxes in  \(\lambda\) such that 
(1) all numbers are used (exactly once),
(2) each row is increasing, and
(3) each column is increasing. We can similarly define (SYT) for skew shapes (difference of two partitions).
</p>

<div class="compute">
  <script type="text/x-sage">
def SYT(Tab,smallest_num = 1):
    # we fill 1, and then 2, etc.
    # find boxes can be filled by the smallest_num
    corner_boxes = []
    for i in range(len(Tab)): 
        try:
            ind = Tab[i].index(None) # find the first index of None in the i th row of Tab 
            if i==0 or Tab[i-1][ind] != None: 
                corner_boxes.append((i,ind))
        except ValueError:
            pass
    if len(corner_boxes)==0: 
        # last step = no box can be filled
        yield Tab 
        return 
    for (i,j) in corner_boxes:
        newTab = [row[:] for row in Tab] 
        # row[:] means making a copy of the row
        # entirely, we made a copy of Tab
        newTab[i][j] = smallest_num
        # Now the smallest_num is filled, we can fill the rest by induction
        for T in SYT(newTab,smallest_num+1): 
            yield T
            
for T in SYT([[0,0,None,None],[0,None,None]]):
    for row in T:
        print(row)
    print("-" * 20)
</script>
</div>

	
<hr>
<a href="/">home</a>
</body>
</html>
