<!DOCTYPE HTML>
<html lang="en">
<head>
<meta charset="utf-8">
<meta name="viewport" content="width=device-width, initial-scale=1.0">
<link rel="stylesheet" href="style.css">
  
<title>Tutorial on SageMath</title>

<script src="https://sagecell.sagemath.org/static/embedded_sagecell.js"></script>
<script>
    sagecell.makeSagecell({inputLocation:  '#Activate',
          template: sagecell.templates.minimal,
          linked: true,
          linkKey:"1",
          autoeval:true,
          hide:["fullScreen"],
          evalButtonText: 'Activate'});
    sagecell.makeSagecell({inputLocation: 'div.compute',
          linked: true,
          linkKey:"1",
          hide:["permalinkpermalink","done","sessionFiles"], 
          evalButtonText: 'Evaluate'});
</script>
</head>
<body>

<h2>Tutorial on SageMath</h2>
	
<h3>Introduction</h3>
  <p><a href="https://www.sagemath.org/index.html">SageMath</a> is mainly a computer algebra system based on <a href="https://www.python.org/">Python</a>, which is very popular in discrete mathematics. SageMath is a free open-source software. 
    As a "consequence", not all of the <a href="https://doc.sagemath.org/html/en/index.html">documentations</a> are written for generic computer users (see this <a href="https://www.reddit.com/r/ProgrammerHumor/comments/7r3hvl/if_the_authors_of_programming_textbooks_wrote/" title = "If the authors of computer programming books wrote arithmetic text books">meme</a>). 
    The main aim of this article is an attempt of making up the gaps.
</p>

<h3>Getting Start</h3>
<p>
  You can have a quick try of SageMath online via <a href="https://sagecell.sagemath.org/">SageCell</a>. 
  It also provides a way to embed Sage computations into any webpage like this. 
  </p>
  
<div class="compute">
<script type="text/x-sage">
# write your code here
print("hello world!")
</script>
</div>

<p>
  You can also use <a href="https://cocalc.com/">CoCalc</a> in the cloud (online). 
  When using it, it is recommended to start with creating a <a href="https://jupyter.org/">Jupyter Notebook</a> with kernel the lastest SageMath.
  </p>
<p>
  If you do hope to download and install SageMath, it will be complicated. 
  Here are some useful links providing some hints:  
<a href="https://github.com/sagemath/sage-windows/releases">Windows</a>,
<a href="https://www.sagemath.org/download.html">Binaries</a>, 
<a href="https://www.sagemath.org/download-source.html">Sources</a>. 
  After installation, it is strongly suggested to use the shortcut "SageMath n.m Notebook" which links to a Jupyter Notebook. 
  </p>
  
<p>
  There are two official introductory documentations: 
  <ul>
    <li><a href="https://www.sagemath.org/tour-quickstart.html">Quick Start</a></li>
    <li><a href="https://doc.sagemath.org/html/en/a_tour_of_sage/">A Tour of Sage</a></li>
  </ul>
They are good so far. 
The readers are suggested to try SageMath as an advanced calculator by themselves. 
  </p>
  
<div class="compute">
<script type="text/x-sage">
print(sqrt(3)) # square roof of 3
print(5 % 3) # 4 mod 3
print(factorial(4)) # 5!

a = 5
print(a + 2 == 4)
</script>
</div>

<h3>Python syntax</h3>

<p>
  Of course, we are only not using SageMath a calculator. Actually, Sage
Math is based on <a href="https://www.python.org/">Python</a>, and so is its syntax (grammar). So the scientific way is to tell something about Python. 
  See the <a href="https://wiki.python.org/moin/BeginnersGuide">official documentation</a>.
  As Python becomes popular recent years, there are many good sources of learning it.  
</p>

<p>
To proceed this article, we will assume the readers know the following 
<ul>
  <li>string, list, dictionary</li>
  <li>if (condition), for (loop), def (function)</li>
</ul>
Here are two (mathematical) examples. 
</p>


<h4>Example: Collatz conjecture</h4>
<p>Consider the following operation on an arbitrary positive integer:
<a>\[n\longmapsto \begin{cases}
	n/2, & \text{if $n$ is even}, \\
	3n+1, & \text{if $n$ is odd}. 
	\end{cases}\]</a>
The Collatz conjecture is: This process will eventually reach the cycle $1\mapsto 4\mapsto 2 \mapsto \cdots$, regardless of which positive integer is chosen initially.
	</p>
<div class="compute">
  <script type="text/x-sage">
num = 100
num_step = 0 # to record the number of steps
while num not in [1,2,4]: 
	num_step += 1 # equivalently, num_step = num_step + 1
	if num/2 in ZZ: # in Sage, ZZ = the ring of integer numbers
		num = num/2
	else:
		num = 3*num + 1
	print("the num is "+str(num))
print("It takes "+str(num_step)+" steps")
</script>
</div>

<h4>Example: Fibonacci sequence</h4>
<p>Fibonacci sequence is the sequence defined by 
\[F_0=0,F_1=1, \text{ and } F_n=F_{n-1}+F_{n-2} \text{ when $n\geq 2$}.\]
We can compute it using the following code. </p>

<div class="compute">
  <script type="text/x-sage">
record={0:0,1:1} # F_0=1, and F_1=1
def Fib(n): # define a function
	if n not in record.keys():
		record[n] = Fib(n-1) + Fib(n-2)
	return record[n] 
for n in range(10): 
	print("F_"+str(n)+" is "+str(Fib(n)))
print(record)
</script>
</div>

<h3>Mathematical structures
</h3>

<p>
Here are some pages frequently used: 
    <ul>
      <li><a href="https://doc.sagemath.org/html/en/constructions/linear_algebra.html">Linear Algebra</a></li>
      <li><a href="https://doc.sagemath.org/html/en/reference/groups/index.html">Group</a></li>
      <li><a href="https://doc.sagemath.org/html/en/reference/polynomial_rings/index.html">Polynomials</a></li>
      <li><a href="https://doc.sagemath.org/html/en/reference/combinat/sage/combinat/posets/all.html#sage-combinat-posets-all">Posets</a></li>
      <li><a href="https://doc.sagemath.org/html/en/reference/graphs/index.html">Graphs</a></li>
  </ul>
Here I list some basis example I used a lot.
</p>

<div class="compute">
  <script type="text/x-sage">
M = Matrix([[binomial(i,j) for j in [0..4]] for i in [0..4]])
N = Matrix([[(-1)^(i+j)*binomial(i,j) for j in [0..4]] for i in [0..4]])
# Note that in Python, everything starts from 0, including matrices
print(det(M*N))
print(M[2][1],N[4,2])
</script>
</div>

<p></p>

<div class="compute">
  <script type="text/x-sage">
R = PolynomialRing(QQ,["x"+str(i) for i in [0..3]])
x = list(R.gens())
f = 2*x[0]^2*x[2] + R.monomial(*[4-i for i in [0..3]])
print(dict(f))
# However, the keys of f.dict() are ETtuple's, and SageMath forgets to include a constructor and overload operators with the usual tuples. 
My_dict = f.dict()
My_dict = {tuple(x): My_dict[x] for x in My_dict}
print((2,0,1,0) in f.dict().keys())
print((2,0,1,0) in My_dict.keys())
</script>
</div>

<p></p>

<div class="compute">
  <script type="text/x-sage">
G=Permutations(3) 
x = G([1,3,2]); y = G([2,1,3])
print(x*y,(x*y).length()) 
print(list([x(y(i)) for i in [1..3]]))
# In SageMath, multiplication of permutations are reversed. 
</script>
</div>

<p></p>

<div class="compute">
  <script type="text/x-sage">
G = Permutations()
X = CombinatorialFreeModule(QQ,G,prefix="e")
e = X.basis()
f = e[G([1,3,2])]+2*e[Permutation([(1,2,5),(3,4)])]
print(dict(f))
# You have to use hashable classes
</script>
</div>

<h4>Example: divided difference</h4>

<p>
Let us consider the divided difference operator
\[\partial_{a,b}f=\frac{f-f|_{x_a\leftrightarrow x_b}}{x_a-x_b}.\]
We take the convention that \(\partial_i=\partial_{i,i+1}\).
</p>


<div class="compute">
  <script type="text/x-sage">
R = PolynomialRing(QQ,["x"+str(i) for i in [0..3]])
x = list(R.gens())
def divided_difference(a,b=None):
	if b==None: b=a+1
	def my_fun(f):
		f=R(f) # make it work even when f is a constant...
		newx = x+[] # this makes a copy of x, this is necessary! 
		(newx[a],newx[b])=(x[b],x[a])
		res = (f - f(newx))/(x[a]-x[b])
		return R(res) 
		# still, this makes Sage believe res is in R, instead of Frac(R)...
	return my_fun 
print(divided_difference(1)(x[1]^3))
print(divided_difference(1,3)(x[1]^3))
print(divided_difference(0,3)(1))
</script>
</div>

<h4>Example: Vandermonde determinant</h4>

<p>
Recall the Vandermonde identity 
\[\left|
\begin{matrix}
1 &amp; 1 &amp; \cdots &amp; 1\\
x_1 &amp; x_2 &amp; \cdots &amp; x_n\\
\vdots &amp; \vdots &amp; \ddots &amp; \vdots \\
x_1^{n-1} &amp; x_2^{n-1} &amp; \cdots &amp; x_n^{n-1}\\
\end{matrix}\right|
=\prod_{1\leq i&lt;j\leq n}(x_i-x_j)\]
</p>


<div class="compute">
  <script type="text/x-sage">
n = 4
R = PolynomialRing(QQ,["x"+str(i) for i in range(n)]) # range(n) = [0..n-1]
x = list(R.gens())

M = Matrix([[x[i]^j for i in range(n)] for j in range(n)])
LHS = det(M)

RHS = 1 
for i in range(n):
    for j in [i+1..n-1]: 
        RHS *= x[i] - x[j] # equivalently, RHS = RHS * (x[i] - x[j])

print(LHS==RHS)
</script>
</div>

<p>
    In general, most of the commands/methods you need can be found by searching on the internet (very similar to the situation of LaTeX). Most of them are from the the <a href="https://doc.sagemath.org/html/en/index.html">official documentations</a>. 
  </p>

<h3>Appendix: Advanced Examples</h3>

<p>In the last section, I give a number of advanced examples requiring more knowledge of Python. 
  </p>

<h4>Example: Schur operators</h4>
<p>For a partition \(\lambda\), we define \(u_i\) the operator 
\[u_i\lambda = \begin{cases}
\lambda\cup\text{a box in the $i$-th row},& \text{if it is a partition},\\
0, & \text{otherwise}. 
\end{cases}\]
This operator extends to the free vector space of partitions linearly. 
The Schur operators satisfy the relation of <a href="https://en.wikipedia.org/wiki/Plactic_monoid">Plactic monoid</a>.
	</p>

<div class="compute">
  <script type="text/x-sage">
X = CombinatorialFreeModule(QQ,Partitions(),prefix="")
def B(*my_list): 
    # with *, we use B(2,1), without *, we use B([2,1])
    return X.monomial(Partition(my_list)) 
	# Note that Partition != Partitions.  
def single_Schur_op(i): # adding a box in the i-th row (i = 0, 1, ...);
    def my_fun(x):
        res = X(0)
        my_dict = dict(x)
        for partition in my_dict: 
            new_partition = list(partition)
            if len(new_partition)>i and (i==0 or new_partition[i-1]!=new_partition[i]):
                # if we can add a box in the i-th row
                new_partition[i] +=1
                res += my_dict[partition]*B(*new_partition)
        return res
    return my_fun
def Schur_op(*i_list): 
    if len(i_list)==0: 
        return lambda x:x # lambda x:x is the function sending x to x, i.e. identity function
    return lambda x: single_Schur_op(i_list[0])(Schur_op(*i_list[1:])(x))
print(Schur_op(1,2,1,0)(B(2,2,1)+B(4,3,3)))
</script>
</div>


<h4>Example: Dunkl elements</h4>
<p>For fixed \(n\), the Chevalley formula gives 
\[x_k\mathfrak{S}_w = 
	-\sum_{\begin{subarray}{c}i&lt;k\\\ell(wt_{ik})=\ell(w)+1\end{subarray}}\mathfrak{S}_{wt_{ik}}
	+\sum_{\begin{subarray}{c}i&gt;k\\\ell(wt_{ik})=\ell(w)+1\end{subarray}}\mathfrak{S}_{wt_{ik}}.									 \]
where \(\mathfrak{S}_w\) is the Schubert polynomial, and \(t_{ab}\) is the transposition of $a,b$.
Dunkl element is the operator formally defined as above. 
</p>
	
<div class="compute">
  <script type="text/x-sage">
n=4
G = Permutations(n)
X = CombinatorialFreeModule(QQ,G,prefix="S")
def tran(i,j): 
    t_ij = [i for i in [1..n]]
    (t_ij[i-1],t_ij[j-1]) = (j,i)
    t_ij = Permutation(t_ij)
    return t_ij
def Dunkl(k): 
    def my_func(x=X.monomial(G.one())): 
        res = X(0)
        my_dict = dict(x)
        for w in my_dict: 
            for i in [1..k-1]:
                new_w = tran(i,k)*w # compute wt_ik (SageMath use reversed order)
                if new_w.length() == w.length() + 1:
                        res -= my_dict[w] * X.monomial(new_w)
            for i in [k+1..n]:
                new_w = tran(i,k)*w # compute wt_ik (SageMath use reversed order)
                if new_w.length() == w.length() + 1:
                        res += my_dict[w] * X.monomial(new_w)
        return res
    return my_func    
print(Dunkl(1)(Dunkl(3)()))
print(Dunkl(3)(Dunkl(1)()))
</script>
</div>

	<br>
	
<div class="compute">
  <script type="text/x-sage">
# Please run the previous cell
R = PolynomialRing(QQ,["x"+str(i) for i in [1..n]])
x = list(R.gens()) # note that x[i-1] = xi
def Horner(f): # return (i,f1,f2) such that f(x[0],...)=x[i]*f1(x[i],...)+f2(x[i+1],...)
    x = list(f.parent().gens())
    i = 0; f1 = 0; f2 = f
    while f == f2 and i <= len(x): 
        i += 1
        sub = [x[j] if j>i else 0 for j in range(len(x))]
        f2 = f(sub)
    if i >= len(x): # f(x[0],...) == f(0), so f is a contant
        return (None,0,f.parent().base_ring()(f2))
    else: # otherwise, return (i,f1,f2)
        return (i,R((f-f2)/x[i]),f2)
# f = x[1]*x[2]
# (i,f1,f2)=Horner(f)
# f == x[i]*f1+f2
def Mult(f): 
    f=R(f)
    (i,f1,f2) = Horner(f)
    if i==None: 
        return lambda x = X.monomial(G.one()) : f2*x
    else:
        return lambda x = X.monomial(G.one()): Mult(f1)(Dunkl(i)(x))+Mult(f2)(x)
Mult(x[1]*x[2])()
</script>
</div>


<h4>Example: Multi-sets</h4>
<p>A multi-set with alphabet \(L\) of cardinality \(k\) is an \(S_k\)-orbit of \(L^k\).
When \(L\) is ordered, we can uniquely write such multi-set by \(\{\!\{i_1\leq \cdots\leq i_k\}\!\}\) with all \(i_j\in L\).
</p>
<div class="compute">
  <script type="text/x-sage">
def Msets(L,k):
    # boundary cases:
    # when k == 0, only empty multi-set
    # when L is empty and k!=0, no multi-set
    if k == 0: 
        yield []
        return
    if len(L) == 0:
        return 
    # if the multi-set contains L[0]
    for A in Msets(L,k-1): 
        yield [L[0]]+A
    # if the multi-set does not contain L[0]
    for A in Msets(L[1:],k):
        yield A

for A in Msets([1,2,3],4):
    print(A)
</script>
</div>



<h4>Example: Standard Young Tableaux</h4>

<p>Let \(\lambda\) be a partition. A standard Young tableau (SYT) of \(\lambda\) is a filling of its Young diagram with \(\{1,\cdots,n\}\) for \(n\) the number of boxes in  \(\lambda\) such that 
(1) all numbers are used (exactly once),
(2) each row is increasing, and
(3) each column is increasing. We can similarly define (SYT) for skew shapes (difference of two partitions).
</p>

<div class="compute">
  <script type="text/x-sage">
def SYT(Tab,smallest_num = 1):
    # we fill 1, and then 2, etc.
    # find boxes can be filled by the smallest_num
    corner_boxes = []
    for i in range(len(Tab)): 
        try:
            ind = Tab[i].index(None) # find the first index of None in the i th row of Tab 
            if i==0 or Tab[i-1][ind] != None: 
                corner_boxes.append((i,ind))
        except ValueError:
            pass
    if len(corner_boxes)==0: 
        # last step = no box can be filled
        yield Tab 
        return 
    for (i,j) in corner_boxes:
        newTab = [row[:] for row in Tab] 
        # row[:] means making a copy of the row
        # entirely, we made a copy of Tab
        newTab[i][j] = smallest_num
        # Now the smallest_num is filled, we can fill the rest by induction
        for T in SYT(newTab,smallest_num+1): 
            yield T
            
for T in SYT([[0,0,None,None],[0,None,None]]):
    for row in T:
        print(row)
    print("-" * 20)
</script>
</div>

	
<hr>
<a href="/">home</a>
</body>
</html>
